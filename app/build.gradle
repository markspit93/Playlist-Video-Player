apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.playlist.video.player"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "DEBUG_MODE", "false"
            buildConfigField "String", "PLAYLIST_ENDPOINT", "\"https://raw.githubusercontent.com/markspit93/Playlist-Video-Player/master/\""
        }
        debug {
            buildConfigField "boolean", "DEBUG_MODE", "true"
            buildConfigField "String", "PLAYLIST_ENDPOINT", "\"https://raw.githubusercontent.com/markspit93/Playlist-Video-Player/master/\""
        }
    }

    testOptions {
        animationsDisabled = true
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    // Note: This is required for Android Studio to create Kotlin files in the kotlin package as documented here https://kotlinlang.org/docs/reference/using-gradle.html#android-studio
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

ext {
    anko_version = '0.10.4'

    supportLibVersion = '27.0.2'
    constraintLayoutVersion = '1.1.0-beta4'
    archComponentsVersion = '1.0.0'
    retrofit2Version = '2.3.0'
    rxKotlinVersion = '2.2.0'
    rxAndroidVersion = '2.0.1'
    daggerVersion = '2.14.1'
    glideVersion = '4.5.0'

    stethoVersion = '1.5.0'
    stethoNoOpVersion = '1.1'

    junitVersion = '4.12'
    mockitoVersion = '2.13.0'
    mockitoKotlinVersion = '1.5.0'
    mockWebServerVersion = '3.9.1'
    supportTestVersion = '1.0.1'
    espressoVersion = '3.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7-commons:$anko_version"

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    kapt "android.arch.lifecycle:compiler:$archComponentsVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2Version"

    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    releaseImplementation "net.igenius:stetho-no-op:$stethoNoOpVersion"

    // Unit Testing Dependencies
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"

    // Instrumentation Testing Dependencies
    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "com.android.support.test:runner:$supportTestVersion"
    androidTestImplementation "com.android.support.test:rules:$supportTestVersion"
    androidTestUtil "com.android.support.test:orchestrator:$supportTestVersion"

    // Espresso dependencies
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
}
